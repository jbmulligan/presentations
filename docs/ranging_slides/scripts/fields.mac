
Define Set_Colors 0
Set black 22
Setcolor $black 0 0 0
Set green 23
Set white 24
Set red 25
Setcolor $green 0 255 0
Setcolor $red 255 0 0
Setcolor $white 255 255 255
.

Define Make_Ball 3 angle attenuation window
Assign mbtheta $1*atan(1)/45
Ramp2D f{0} -100 1 0
Ramp2D f{1} -100 0 1
VSMul f f 1.2/100
VVMul g f{1} f{1}
VVMul h f{0} f{0}
VVAdd g g h
VSAdd g g -1
# if it's negative then we're on the sphere
VSMin g g 0
VSMul g g -1
VSqrt f{2} g
#Disp_Gray g probe_v1
#Pause

VSMul g g 10000
VSMin g g 1
#Disp_Gray g probe_v1
#Pause
VVMul f{0} f{0} g
VVMul f{1} f{1} g

#Disp_Gray f{0} probe_v1
#Pause
#Disp_Gray f{1} probe_v1
#Pause

# now light the ball
Assign a sin($mbtheta)
Assign b 0
Assign c cos($mbtheta)

# take the dot product of (a,b,c) with the normal vector
VSMul g f{0} $a
#Disp_Gray g probe_v1
#Pause
VSMul h f{2} $c
#Disp_Gray h probe_v1
#Pause
VVAdd g g h
VSMax g g 0
# g goes from 0 to 1
VSMul g g 255*$2
Convert b g
Show_Bytes b $3
#Disp_Gray g probe_v1

.

Define Plot_Ball_Location 2 x y
view
  draw top_view
    foreground $white
    fill_arc $1-$rad $2-$rad 2*$rad 2*$rad 0 64*360
    #Pause
    quit
  quit
.

Define Ball_At 2 r theta
#advise "Ball_At $1 $2"
Assign ang $2
Assign range $1
Assign bx $xc+$range*sin($ang)
Assign by $yc+$range*cos($ang)
view draw top_view clear quit quit

Draw_Near_Field $xc $yc $green
If $show_second_field "Draw_Near_Field $xc+50 $yc $red"

Plot_Ball_Location $bx $by

Assign atten ($d0*$d0)/($range*$range)
#Print atten
Mul_Var ang 45/atan(1)		# convert to degrees
Make_Ball $ang $atten probe_v1
# now get the coords relative to the second lamp
Mul_Var ang atan(1)/45
Assign bx $range*sin($ang)
Assign by $range*cos($ang)
Add_Var bx -50			# x coord of second lamp
Assign ang 45*atan2($bx,$by)/atan(1)
#advise "ang relative to second illum is $ang"
Assign range sqrt($bx*$bx+$by*$by)
#Print range
Assign atten ($d0*$d0)/($range*$range)
If $show_second_field "Make_Ball $ang $atten probe_v2"
.

# Ball_At2 does the far field...
Set show_second_field 0

Define Ball_At2 2 r theta
#advise "Ball_At $1 $2"
Assign ang $2
Assign range $1
Assign bx $xc+$range*sin($ang)
Assign by $yc+$range*cos($ang)
view draw top_view clear quit quit

Draw_Far_Field $xc $yc $green
If $show_second_field "Draw_Far_Field $xc+50 $yc $red"

Plot_Ball_Location $bx $by

Set atten 1
Mul_Var ang 45/atan(1)		# convert to degrees
Make_Ball $illum_ang1 $atten probe_v1
If $show_second_field "Make_Ball $illum_ang2 $atten probe_v2"
.

Define Draw_Far_Field 3 fx fy color
If $1==$xc
  Then "Set ffa $illum_ang1"
  Else "Set ffa $illum_ang2"
#Print ffa

Assign ffa_dx 100*cos(atan(1)*$ffa/45)
Assign ffa_dy 100*sin(atan(1)*$ffa/45)

view
  draw top_view
    background $black
    foreground $3
    Set x $1
    Set y $2

    repeat 10
      move $x-$ffa_dx $y-$ffa_dy
      cont $x+$ffa_dx $y+$ffa_dy
      Assign ldx -25*sin(atan(1)*$ffa/45)
      Assign ldy 25*cos(atan(1)*$ffa/45)
      advise "ldx $ldx    ldy $ldy"
      Add_Var x $ldx
      Add_Var y $ldy
      end
    quit
  quit
.

Assign contour_factor sqrt(2)
#Assign contour_factor 2
Set sigma 0.1

Define Render_Near_Field 5 field_dest contour_dest fx fy min_range
Make_Radius_At $1 $3 $4
VVMul $1 $1 $1			# distance squared
VSMax $1 $1 $5				# so don't divide by 0
VSDiv $1 $1 1
# now make some contours
Scale $1 1 255
Get_Contours $2 $1
.

Define Get_Contours 2 dst src
Dup_Float rnf_tmp $1
VSet $1 0
Get_Min clevel $2
Mul_Var clevel $contour_factor
verbose yes
repeat 10
  Assign sigma $clevel*$clevel/100
  VMov rnf_tmp $2
  VSAdd rnf_tmp rnf_tmp -$clevel
  VVMul rnf_tmp rnf_tmp rnf_tmp
  VSMul rnf_tmp rnf_tmp -1/(2*$sigma*$sigma)
  VExp rnf_tmp rnf_tmp
  VVAdd $1 $1 rnf_tmp
  Mul_Var clevel $contour_factor
  end
Delete_Image rnf_tmp
.

Define Draw_Near_Field 3 fx fy color
view
  draw top_view
    background $black
    foreground $3
    Set diam 0
    repeat 11
      Assign xl $1-$diam/2
      Assign yu $2-$diam/2
      arc $xl $yu $diam $diam (180+45)*64 (90)*64
      Add_Var diam 50
      end
    # now put a disk at a position...
    quit
  quit
.

Define MoveIt 1 macro
Set n_deg 1.5
Set r 150
Set theta 0
repeat 20
  $1 $r $theta
  Mul_Var r 1.01
  end
repeat 20
#Print theta
  $1 $r $theta
  Add_Var theta $n_deg*atan(1)/45	# 1 deg
  end
repeat 20
  $1 $r $theta
  Mul_Var r 1/1.01
  end
repeat 40
  $1 $r $theta
  Add_Var theta -$n_deg*atan(1)/45
  end
repeat 20
  $1 $r $theta
  Mul_Var r 1.01
  end
repeat 20
#Print theta
  $1 $r $theta
  Add_Var theta $n_deg*atan(1)/45	# 1 deg
  end
.

